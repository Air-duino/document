---
title: 控制1.8' TFT_LCD显示屏
order: 6
icon: desktop
---

:::warning

本文档尚未进行review，可能存有不准确的描述内容。

:::

## 简介

本章介绍使用Air001开发板驱动ST7735。

::: tip

 ST7735是一款具有SPI接口的彩色TFT液晶显示驱动芯片。

:::

## 硬件准备

- 按[☁️ Air001开发板入门](/tutorial-advanced/Air001_start.html)，将`Air001`和`DAPLink调试器`使用排针排母连接。

- 将`ST7735`模块与`Air001开发板`，按如下表格进行相连：

| SHT30 | Air001 |
| :---: | :----: |
|  GND  |  GND   |
|  VCC  |  3.3V  |
|  SCL  |  PA_5  |
|  SDA  |  PA_7  |
|  RES  |  PA_6  |
|  DC   |  PB_1  |
|  CS   |  PA_4  |
|  BL   |  PB_0  |

## 软件部分

在Arduino的库管理中搜索`ST7735`以安装`Adafruit ST7735 and ST7789 Library`，并选择“全部安装”以安装所需依赖。

代码如下:

```cpp
#include <Adafruit_GFX.h>
#include <Adafruit_ST7735.h>
#include <SPI.h>

#define TFT_CS PA_4
#define TFT_RST PA_6
#define TFT_DC PB_1
#define TFT_MOSI PA_7
#define TFT_SCLK PA_5
#define SerialDebugging true

Adafruit_ST7735 tft = Adafruit_ST7735(TFT_CS, TFT_DC, TFT_MOSI, TFT_SCLK, TFT_RST);

const uint8_t Button_pin = PB_6;
```

- 开头我们用调用下载的`Adafruit_GFX.h`、`Adafruit_ST7735.h`包以及Arduino自带的`SPI.h`包。
- 依据Air001的接线定义引脚。
- 实例化同时初始化`Adafruit_ST7735`的对象`tft`，后续仅需调用`tft`即可驱动`ST7735`。
- 将Air001开发板上的`BOOT`按钮对应的引脚`PB_6`定义为屏幕开关。

```cpp
const uint16_t Display_Color_Black = 0x0000; //黑
const uint16_t Display_Color_Blue = 0x001F;  //蓝

uint16_t Display_Text_Color = Display_Color_Black;
uint16_t Display_Backround_Color = Display_Color_Blue;
```

- 我们用`cosnt`定义颜色并防止其被更改。
- 定义文本颜色和背景颜色。

::: tip

可以使用(包括但不限于)如下代码定义其他颜色:

```cpp
const uint16_t Display_Color_Red = 0xF800;      //红
const uint16_t Display_Color_Cyan = 0x07FF;     //青
const uint16_t Display_Color_Green = 0x07E0;    //绿
const uint16_t Display_Color_White = 0xFFFF;    //白
const uint16_t Display_Color_Yellow = 0xFFE0;   //黄
const uint16_t Display_Color_Magenta = 0xF81F;  //粉
```

:::

```cpp
bool isDisplayVisible = false;
const size_t MaxString = 32;
char oldTimeString[MaxString]           = { 0 };
volatile bool   isButtonPressed         = false;
```

- 定义逻辑型变量`isDisplayVisible`判断屏幕是否显示并赋予初值`false`。
- 声明缓冲区大小。
- 初始显示空的字符串。
- 声明逻辑型变量`isButtonPressed`判断按钮是否被按下并赋予初值`false`。

```cpp
void senseButtonPressed() {
    if (!isButtonPressed) {
        isButtonPressed = true;
    }
}
```

定义函数`senseButtonPressed`，在`false`状态下按下按钮后将`isButtonPressed`的值变为`true`。

```cpp
void displayUpTime() {
    unsigned long upSeconds = millis() / 1000;
    unsigned long days = upSeconds / 86400;
    upSeconds = upSeconds % 86400;
    unsigned long hours = upSeconds / 3600;
    upSeconds = upSeconds % 3600;
    unsigned long minutes = upSeconds / 60;
    upSeconds = upSeconds % 60;
    char newTimeString[MaxString] = { 0 };
    sprintf(
        newTimeString,
        "%lu %02lu:%02lu:%02lu",
        days, hours, minutes, upSeconds
    );
    if (strcmp(newTimeString,oldTimeString) != 0) {
        tft.setCursor(0,0);
        tft.setTextColor(Display_Backround_Color);
        tft.print(oldTimeString);
        tft.setCursor(0,0);
        tft.setTextColor(Display_Text_Color);
        tft.print(newTimeString);
        strcpy(oldTimeString,newTimeString);
    }
}
```

定义函数`displayUpTime`，计算显示的天数、小时、分钟、秒钟,将其显示并刷新(每次刷新消耗约2.5s)。

我们在`setup()`函数中添加如下代码进行初始化操作。

```cpp
void setup() {
    pinMode(Button_pin,INPUT_PULLUP);
    attachInterrupt(digitalPinToInterrupt(Button_pin), senseButtonPressed, FALLING);
    #if (SerialDebugging)
    Serial.begin(115200);
    while (!Serial);
    Serial.println();
    #endif
    delay(250);
    isButtonPressed = false;
    tft.initR(INITR_BLACKTAB);
    tft.setFont();
    tft.fillScreen(Display_Backround_Color);
    tft.setTextColor(Display_Text_Color);
    tft.setTextSize(2);
    isDisplayVisible = true;
}
```

- 通过`pinMode`将按钮引脚`PB_6`设定为`INPUT_PULLUP`上拉模式。
- 用按钮控制屏幕显示。
- 开头`SerialDebugging`就被赋值为`TRUE`，运行下一步设定波特率为115200。
- 延时。
- 将`false`重新赋予`isButtonPressed`。
- 用`initR`初始化1.8寸屏幕。
- 用`fillScreen`来用蓝色填充屏幕，用`setTextColor`设定文字颜色，并用`setTextSize`设定文字大小。
- 将`true`赋给`isDisplayVisible`来使屏幕显示。

最后在`loop()`函数中添加代码。

```cpp
void loop() {
    displayUpTime();
    if (isButtonPressed) {
        isDisplayVisible = !isDisplayVisible;
        tft.enableDisplay(isDisplayVisible);
        #if (SerialDebugging)
        Serial.print("button pressed @ ");
        Serial.print(millis());
        Serial.print(", display is now ");
        Serial.println((isDisplayVisible ? "ON" : "OFF"));
        #endif
        isButtonPressed = false;
    }
    delay(100);
}
```

- 调用函数`displayUpTime`输出显示时长。
- 按下按钮后将`isDisplayVisible`的值变为`非isDisplayVisible`，以达到按按钮控制屏幕开关的效果
- 延时

## 输出结果

屏幕底色为蓝色，在最上方显示一行黑色的显示时长:
![](img/ST7735_on.jpg)
使用按钮后屏幕变为白色，不显示任何文字:
![](img/ST7735_off.jpg)
同时在串口监视器中将波特率调为115200可观察到如下字样:
![](img/ST7735_b.png)
